/*
 *  Copyright 2020 Bammellab / James Andreas
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *       http://www.apache.org/licenses/LICENSE-2.0
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'


def versionMajor = 2
def versionMinor = 1
def versionPatch = 0
def versionBuild = 2101

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        applicationId "com.bammellab.motm"

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        resConfigs 'en'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildFeatures {
        dataBinding = true
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles(
                getDefaultProguardFile('proguard-android-optimize.txt'),
                'proguard-rules.pro',
                'src/main/proguard-motmbrowser.pro'
            )
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
        }
    }

    buildFeatures {
        viewBinding true // https://developer.android.com/topic/libraries/view-binding
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        lintConfig rootProject.file('lint.xml')
        disable "UnusedResources",
            "VectorRaster",
            "StaticFieldLeak",
            "UseCompoundDrawables",
            "TypographyEllipsis",
            "IconLauncherShape",
            "IconLocation",
            "Autofill",
            "RtlEnabled"

        checkDependencies true
        checkTestSources true
        explainIssues false

        // from Jake Wharton - not true yet for this project:
        // We run a full lint analysis as build part in CI, so skip vital checks for assemble task.
        checkReleaseBuilds false

        // added to keep going
        abortOnError false
    }
}



// signing solution courtesy of http://gist.github.com/gabrielemariotti/6856974

Properties props = new Properties()
def propFile = new File('gradle/signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'gradle/signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'gradle/signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation project(':mollib')
    implementation deps.kotlin.stdlib.jdk
    implementation deps.kotlin.coroutines.jdk
    implementation deps.kotlin.coroutines.android

    implementation deps.android.material
    implementation deps.android.x.appcompat
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation deps.android.x.coreKtx
    implementation deps.android.x.cardview
    implementation deps.android.x.lifecycle
    implementation deps.android.x.navigationfrag
    implementation deps.android.x.navigationui
    implementation deps.android.x.preference

    implementation deps.okhttp.client
    implementation deps.retrofit.client
    implementation deps.gson

    implementation deps.pdbparser
    implementation deps.timber
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation deps.guava
    implementation deps.glide
    kapt deps.glide

    testImplementation deps.annotations
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'com.google.truth:truth:1.1'
}
