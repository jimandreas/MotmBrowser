/*
 *  Copyright 2022 Bammellab / James Andreas
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *       http://www.apache.org/licenses/LICENSE-2.0
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License
 */

/*
  Changes Made

  1. motmbrowser/build.gradle
  - Set minifyEnabled true
  - Set shrinkResources true
  - Removed reference to non-existent proguard-rules.pro

  2. motmbrowser/src/main/proguard-motmbrowser.pro
  - Added Retrofit keep rules
  - Added Gson keep rules
  - Added @Keep annotation support

  3. CLAUDE.md
  - Added bundleRelease command
  - Added new "Release Build with R8 Obfuscation" section explaining:
    - How R8 works
    - Automatic mapping file inclusion in AAB (AGP 8.13.2+)
    - Location of mapping files for local debugging

  Verification Results

  - Build successful with R8 minification (minifyReleaseWithR8 task executed)
  - Mapping file generated: mapping.txt (20MB)
  - Release APK size: 2.2MB
  - Google Play will automatically use the mapping file from the AAB to deobfuscate crash reports
 */
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}


def versionMajor = 2
def versionMinor = 9
def versionPatch = 0
def versionBuild = 2901

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        applicationId "com.bammellab.motm"

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        resourceConfigurations += ['en']

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildFeatures {
        dataBinding = true
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles(
                getDefaultProguardFile('proguard-android-optimize.txt'),
                'src/main/proguard-motmbrowser.pro'
            )
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
        }
    }

    buildFeatures {
        viewBinding true // https://developer.android.com/topic/libraries/view-binding
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    lint {
        abortOnError false
        checkDependencies true
        checkReleaseBuilds false
        checkTestSources true
        disable 'UnusedResources', 'VectorRaster', 'StaticFieldLeak', 'UseCompoundDrawables', 'TypographyEllipsis', 'IconLauncherShape', 'IconLocation', 'Autofill', 'RtlEnabled'
        explainIssues false
        lintConfig file('lint.xml')
        textOutput file('stdout')
        textReport true
    }
    namespace 'com.bammellab.motm'

}


// signing solution courtesy of http://gist.github.com/gabrielemariotti/6856974

Properties props = new Properties()
def propFile = new File('gradle/signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'gradle/signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'gradle/signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation project(':mollib')
    implementation deps.kotlin.stdlib.jdk
//    implementation deps.kotlin.coroutines.jdk
    implementation deps.kotlin.coroutines.android

    implementation deps.android.material
    implementation deps.android.x.appcompat
    implementation deps.android.x.constraintLayout
    implementation deps.android.x.coreKtx
    implementation deps.android.x.cardview
    implementation deps.android.x.lifecycleKtx
    implementation deps.android.x.navigationfragKtx
    implementation deps.android.x.navigationuiKtx
    implementation deps.android.x.preferenceKtx
    implementation deps.android.x.viewpager2

    implementation deps.okhttp.client
    implementation deps.retrofit.client
    implementation deps.gson

    implementation deps.pdbparser
    implementation deps.timber
    implementation deps.picasso
    // implementation deps.guava
    implementation deps.glide
    kapt deps.glide

    testImplementation deps.annotations
    testImplementation deps.jupiter
    testImplementation deps.truth
}
